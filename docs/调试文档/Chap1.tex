\chapter{仿真调试}

\section{简介概述}

在开发的任何时间周期内，尤其是开发前期阶段，仿真调试都起到了至关重要、不可替代的作用。这里的仿真主要是指功能仿真，不考虑硬件的时序约束。

仿真调试有以下优点：

（1）无需编译。在开发后期，无论多么微小的改动，Vivado在高性能笔记本上完整编译一次（合成、实现、生成比特流）至少需要10分钟！尤其是编译到一半发现代码写错重新编译是最令人绝望的事。而仿真无需编译，如果无法仿真，报错信息相比编译报错信息非常友好。

（2）信息全部透明。可以单步调试，所有数据的信息全部可以通过仿真窗口完整的查看，比通过LED灯或者七段数码管显示友好太多。

（3）速度快。仿真过程中一次仿真几百万ns瞬间就能完成，更可以随时回退，重新仿真。

仿真调试有以下缺点：

（1）无时序约束。功能仿真不考虑时序，有可能因为时序的问题（如数据建立时间、数据保持时间没有考虑），仿真结果正确而实际运行不正确。

事实上，在笔者的开发过程中，绝大多数情况下，仿真和板子实际运行的结果是相同的。

仿真可以说是硬件调试界的gdb，具有无可比拟的强大优势，硬件开发必须熟练掌握这一武器。

\section{具体实现}

\subsection{搭建仿真平台}

想要进行仿真调试，第一步便是搭建仿真平台，即搭建SOPC。

笔者在整个开发阶段中共搭建了两个SOPC。

初期阶段，SOPC主要包含CPU，配合上指令存储器、数据存储器，主要测试的是CPU能不能执行正确单条指令，代码如下，相信聪明的读者一定能一眼看懂。

\begin{lstlisting}[language=verilog]
`include "defines.v"

module openmips_min_sopc(
    input wire clk,
    input wire rst
);

    wire[`InstAddrBus] inst_addr;
    wire[`InstBus] inst;
    wire rom_ce;
    wire mem_we_i;
    wire[`RegBus] mem_addr_i;
    wire[`RegBus] mem_data_i;
    wire[`RegBus] mem_data_o;
    wire[3:0] mem_sel_i;
    wire mem_ce_i;
    wire[5:0] int;
    wire timer_int;

    assign int = {5'b00000, timer_int};

    openmips openmips0(
        .clk(clk),
        .rst(rst),
        .if_addr_o(inst_addr),
        .if_data_i(inst),
        .if_ce_o(rom_ce),
        .mem_we_o(mem_we_i),
        .mem_addr_o(mem_addr_i),
        .mem_sel_o(mem_sel_i),
        .mem_data_o(mem_data_i),
        .mem_data_i(mem_data_o),
        .mem_ce_o(mem_ce_i),
        .int_i(int),
        .timer_int_o(timer_int)
    );

    inst_rom inst_rom0(
        .addr(inst_addr),
        .inst(inst),
        .ce(rom_ce)
    );

    data_ram data_ram0(
        .clk(clk),
        .we(mem_we_i),
        .addr(mem_addr_i),
        .sel(mem_sel_i),
        .data_i(mem_data_i),
        .data_o(mem_data_o),
        .ce(mem_ce_i)
    );

endmodule // openmips_min_sopc
\end{lstlisting}

\begin{lstlisting}[language=verilog]
`include "defines.v"

module inst_rom(
    input wire ce,
    input wire[`InstAddrBus] addr,
    output reg[`InstBus] inst
);

    reg[`InstBus] inst_mem[0:`InstMemNum-1];

    initial $readmemh ("inst_rom.data", inst_mem);

    always @(*) begin
        if (ce == `ChipDisable) begin
            inst <= `ZeroWord; 
        end else begin
            inst <= inst_mem[addr[`InstMemNumLog2+1:2]];
        end
    end

endmodule // inst_rom
\end{lstlisting}

\begin{lstlisting}[language=verilog]
`include "defines.v"
module data_ram(
    input wire clk,
    input wire ce,
    input wire we,
    input wire[`DataAddrBus] addr,
    input wire[3:0] sel,
    input wire[`DataBus] data_i,
    output reg[`DataBus] data_o
);
    reg[`ByteWidth] data_mem0[0:`DataMemNum-1];
    reg[`ByteWidth] data_mem1[0:`DataMemNum-1];
    reg[`ByteWidth] data_mem2[0:`DataMemNum-1];
    reg[`ByteWidth] data_mem3[0:`DataMemNum-1];

    always @(posedge clk) begin
        if (ce == `ChipDisable) begin
            data_o <= `ZeroWord;
        end else if (we == `WriteEnable) begin
            if (sel[3] == 1'b1) begin
                data_mem3[addr[`DataMemNumLog2+1:2]] <= data_i[31:24]; 
            end
            if (sel[2] == 1'b1) begin
                data_mem2[addr[`DataMemNumLog2+1:2]] <= data_i[23:16]; 
            end
            if (sel[1] == 1'b1) begin
                data_mem1[addr[`DataMemNumLog2+1:2]] <= data_i[15:8]; 
            end
            if (sel[0] == 1'b1) begin
                data_mem0[addr[`DataMemNumLog2+1:2]] <= data_i[7:0]; 
            end
        end
    end

    always @(*) begin
        if (ce == `ChipDisable) begin
            data_o <= `ZeroWord;
        end else if (we == `WriteDisable) begin
            data_o <= {data_mem3[addr[`DataMemNumLog2+1:2]], data_mem2[addr[`DataMemNumLog2+1:2]], data_mem1[addr[`DataMemNumLog2+1:2]], data_mem0[addr[`DataMemNumLog2+1:2]]};
        end else begin
            data_o <= `ZeroWord; 
        end
    end

endmodule // data_ram
\end{lstlisting}

后期阶段，SOPC主要包含了内含CPU的thinpad\_top，thinpad\_top是硬件端的顶层文件，配合上模拟硬件实现的Flash、RAM，实现过程中严格按照硬件的实际情况实现，保证接口、大小端、编址方式全部和硬件相同，这样可以保证，不考虑时序的前提下，仿真的结果和硬件结果完全相同。

\begin{lstlisting}[language=verilog]
`include "defines.v"
`timescale 1ns/1ps

module thinpad_min_sopc();

    wire [31:0] ram_data;
    wire [19:0] ram_addr;
    wire [3:0] ram_be_n;
    wire ram_ce_n;
    wire ram_we_n;
    wire [31:0] ext_ram_data;
    wire [19:0] ext_ram_addr;
    wire [3:0] ext_ram_be_n;
    wire ext_ram_ce_n;
    wire ext_ram_we_n;
    wire [5:0] touch_btn;
    wire [22:0] flash_addr;
    wire [15:0] flash_data;

    reg clk;
    reg clk_25;
    reg CLOCK_11059200;
    reg rst;

    initial begin
        clk = 1'b0; 
        forever #10 clk = ~clk; 
    end
    initial begin
        clk_25 = 1'b0;
        forever #20 clk_25 = ~clk_25;
    end
    initial begin
        CLOCK_11059200 = 1'b0;
        forever #45.211227 CLOCK_11059200 = ~CLOCK_11059200;
    end

    initial begin
        rst = 1'b1;
        #195 rst = 1'b0;
        #500000000 $stop; 
    end

    assign touch_btn = {rst, 5'b00000};

    thinpad_top thinpad_top0(
        .clk_in(clk),
        .clk_uart_in(CLOCK_11059200),
        .touch_btn(touch_btn),
        .base_ram_data(ram_data),
        .base_ram_addr(ram_addr),
        .base_ram_be_n(ram_be_n),
        .base_ram_we_n(ram_we_n),
        .base_ram_ce_n(ram_ce_n),
        .ext_ram_data(ext_ram_data),
        .ext_ram_addr(ext_ram_addr),
        .ext_ram_be_n(ext_ram_be_n),
        .ext_ram_we_n(ext_ram_we_n),
        .ext_ram_ce_n(ext_ram_ce_n),
        .flash_data(flash_data),
        .flash_a(flash_addr)

    );
    
    flash flash0(
        .clk(clk),
        .a(flash_addr[22:1]),
        .data(flash_data)
    );

    ram ram0(
        .clk(clk),
        .we(ram_we_n),
        .addr(ram_addr),
        .be(ram_be_n),
        .data(ram_data),
        .ce(ram_ce_n)   
    );

    ram ram1(
        .clk(clk),
        .we(ext_ram_we_n),
        .addr(ext_ram_addr),
        .be(ext_ram_be_n),
        .data(ext_ram_data),
        .ce(ext_ram_ce_n)      
    );

endmodule // openmips_min_sopc
\end{lstlisting}

\begin{lstlisting}[language=verilog]
module ram(
    input wire clk,
    input wire ce,
    input wire we,
    input wire oe,
    input wire[19:0] addr,
    input wire[3:0] be,
    inout wire[31:0] data
);
    reg[31:0] data_ram[0:1048575];
    
    // initial $readmemh ("ram.data", data_ram);

    assign oe = 1'b0;

    always @(posedge clk) begin
        if (ce == 1'b1) begin
            
        end else if (we == 1'b0) begin
            if (be[3] == 1'b0) begin
                data_ram[addr][7:0] <= data[31:24]; 
            end
            if (be[2] == 1'b0) begin
                data_ram[addr][15:8] <= data[23:16]; 
            end
            if (be[1] == 1'b0) begin
                data_ram[addr][23:16] <= data[15:8]; 
            end
            if (be[0] == 1'b0) begin
                data_ram[addr][31:24] <= data[7:0]; 
            end
        end
    end

    assign data = we? { data_ram[addr][7:0], data_ram[addr][15:8], data_ram[addr][23:16], data_ram[addr][31:24] }: 32'bzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzzz;

endmodule // data_ram
\end{lstlisting}

\begin{lstlisting}[language=verilog]
module flash(
    input wire clk,
    input wire ce,
    input wire we,
    input wire oe,
    input wire rp,
    input wire byte,
    input wire[21:0] a,
    inout wire[15:0] data
);
    reg[15:0] data_flash[0:4194303];

    initial $readmemh ("flash.data", data_flash);

    assign rp = 1'b1;
    assign byte = 1'b1;
    assign we = 1'b1;
    assign oe = 1'b0;
    assign ce = 1'b0;

    assign data = { data_flash[a][7:0], data_flash[a][15:8] };

endmodule // data_flash
\end{lstlisting}

\subsection{初始化}

初始化主要有以下内容：

设置仿真时间上界。

设置clk时钟信号，这里时钟频率可以是任意的，因为功能仿真并没有考虑时序。

设置rst重置信号，这里的重置信号在某一时间从1跳变为0，CPU开始运行。

通过initial命令进行信号的初始化。

通过文件配合initial命令对存储器进行数据的初始化。

\subsection{时钟单步调试}

在搭建仿真平台并初始化后，下一步便是打开进入激动人心的仿真了！

先重置到0ns，然后根据仿真设置的clk时钟频率，例如25MHz的时钟频率就对应40ns一个时钟周期，开始进行单步调试，单步调试时可以通过左侧的窗口详细完整的看到所有寄存器的数据信息。

通过跟踪这些状态信息，读者就可以清楚地定位到某一条指令执行是否正确，某一模块实现是否存在异常。

总体而言，仿真调试好比一个图形化的gdb，可以帮助开发人员在任何阶段快速定位到错误信息。

\subsection{时钟快速调试}

本部分调试适用于开发中后期，待测试指令条数非常多时。

例如运行功能测例时，每通过一个指令19号寄存器值加1，由于测试指令有很多很多条，可能执行10000条指令才能出现想看到的结果（寄存器值变化），对此可以根据仿真设置的clk时钟频率，快速推进十万个时钟周期。

如果结果和预期不符，利用二分法找出错误位置，再重复时钟单步调试过程。
